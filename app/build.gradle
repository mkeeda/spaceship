import dev.mkeeda.spaceship.buildsrc.Libs

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

def useReleaseKeystore = USE_RELEASE_KEY_STORE.toBoolean()

android {
    compileSdk 31

    defaultConfig {
        applicationId "dev.mkeeda.spaceship"
        minSdk 26
        targetSdk 31
        versionCode VERSION_CODE as int
        versionName VERSION_NUMBER.toString() + "-" + BUILD_NUMBER.toString()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        debug {
            storeFile file("${System.properties['user.home']}/.android/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }

        release {
            if (useReleaseKeystore) {
                storeFile rootProject.file("./keystore/spaceship-upload-keystore.jks")
                storePassword propertyOrDefault('RELEASE_KEYSTORE_PASSWORD', "")
                keyAlias propertyOrDefault('RELEASE_KEY_ALIAS', "")
                keyPassword propertyOrDefault('RELEASE_KEY_PASSWORD', "")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            if (useReleaseKeystore) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion Libs.AndroidX.Compose.compilerVersion
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
            excludes += 'META-INF/gradle/incremental.annotation.processors'
        }
    }
}

dependencies {
    implementation project(":ui:common")
    implementation project(":ui:timeline")
    implementation project(":domain")
    implementation project(":data")
    implementation project(":infra")

    implementation Libs.AndroidX.coreKtx
    implementation Libs.AndroidX.Compose.compiler
    implementation Libs.AndroidX.Compose.ui
    implementation Libs.AndroidX.Compose.material
    implementation Libs.AndroidX.Compose.uiToolingPreview
    implementation Libs.AndroidX.lifecycleRuntimeKtx
    implementation Libs.AndroidX.activityCompose
    implementation Libs.AndroidX.navigationCompose
    implementation Libs.AndroidX.constraintLayoutCompose

    implementation Libs.Accompanist.inset
    implementation Libs.Accompanist.insetUi
    implementation Libs.Accompanist.systemUiController

    implementation Libs.Dagger.Hilt.base
    kapt Libs.Dagger.Hilt.compiler

    testImplementation Libs.Test.junit

    androidTestImplementation Libs.AndroidX.Test.extJunit
    androidTestImplementation Libs.AndroidX.Test.espressoCore
    androidTestImplementation Libs.AndroidX.Compose.uiTestJunit4

    debugImplementation Libs.AndroidX.Compose.uiTooling
}
